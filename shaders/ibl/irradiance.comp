#version 460 core
// clang-format off
{% include "consts.glsl" %}
{% include "ibl/cubemap_to_xyz.glsl" %}

layout(local_size_x = {{ consts.ibl.local_size_xy }}, local_size_y = {{ consts.ibl.local_size_xy }}
    , local_size_z = {{ consts.ibl.local_size_z }}) in;
// clang-format on

layout(binding = 0) uniform samplerCube cubemap;
layout(rgba32f, binding = 1) uniform writeonly imageCube irradianceMap;

uniform float sampleDelta;
uniform uint offset_x;
uniform uint offset_y;

// Based on https://learnopengl.com/PBR/IBL/Diffuse-irradiance
void main()
{
    uvec3 globalId = gl_GlobalInvocationID.xyz;
    globalId.x += offset_x;
    globalId.y += offset_y;
    vec3 normal = directionFromCubemapUv(globalId, imageSize(irradianceMap).x);

    // Create basis vectors
    vec3 up = vec3(0., 1.0, 0.);
    vec3 right = normalize(cross(up, normal));
    up = normalize(cross(normal, right));

    vec3 irradiance = vec3(0.);

    int sampleCount = 0;

    for (float phi = 0.; phi < 2. * PI; phi += sampleDelta) {
        for (float theta = 0.; theta < 0.5 * PI; theta += sampleDelta) {
            // Spherical coordinates to cartesian
            vec3 baseSampleDir = vec3(sin(theta) * cos(phi), sin(theta) * sin(phi), cos(theta));
            // Change of basis from canonical to the basis of the normal
            vec3 sampleDir = baseSampleDir.x * right + baseSampleDir.y * up + baseSampleDir.z * normal;

            // cos(theta) is Lambert's law
            // sin(theta) arises due to how integrating over the hemisphere works
            irradiance += texture(cubemap, sampleDir).rgb * cos(theta) * sin(theta);
            sampleCount++;
        }
    }

    vec4 color = vec4(PI * irradiance / float(sampleCount), 1.);
    imageStore(irradianceMap, ivec3(globalId), color);
}
